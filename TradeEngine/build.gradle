buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.9.1'
    }
}

plugins {
    id 'java'
    id 'com.google.protobuf' version '0.9.1'
}

group 'com.zeromb.gotostock'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    //gRPC
    runtimeOnly 'io.grpc:grpc-netty-shaded:1.51.0'
    implementation 'io.grpc:grpc-protobuf:1.51.0'
    implementation 'io.grpc:grpc-stub:1.51.0'
    compileOnly 'org.apache.tomcat:annotations-api:6.0.53'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

test {
    useJUnitPlatform()
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.21.7"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.51.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

jar {
    destinationDirectory.set(file(projectDir.toPath().toString() + '/TE dir'))

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes (
                "Main-Class": "com.zeromb.gotostock.Main")
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}